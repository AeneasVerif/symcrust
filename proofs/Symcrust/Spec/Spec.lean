import Mathlib.Data.List.Defs
import Mathlib.Data.ZMod.Defs
import Mathlib.LinearAlgebra.Matrix.Defs
import Mathlib.LinearAlgebra.Matrix.RowCol
import Mathlib.Tactic.IntervalCases
import Aeneas
import Symcrust.Spec.NatBit
import Symcrust.Spec.Round
import Symcrust.Spec.Utils
import Symcrust.Spec.Sha3XOF

/-!
The spec of ML-KEM, based on: https://csrc.nist.gov/pubs/fips/203/final
-/

namespace Symcrust.Spec

open Aeneas.Notations.SRRange  -- allows the `[0:256:2*len]` notations to desugar to an `SRRange` instead of a `Std.Range`
open Aeneas.Notations.DivRange -- activates the `[start : >stop : /divisor]` notation
open Aeneas.Notations.MulRange -- activates the `[start : <stop : *mul]` notation

namespace Notations

  -- Overloading the `get_elem_tactic` so that notation `l[i]` works
  scoped macro_rules
  | `(tactic| get_elem_tactic) => `(tactic| scalar_tac)

  @[scalar_tac]
  theorem div_range_in_bounds {len start : â„•}
    (h0 : 1 < len âˆ§ len â‰¤ 128 âˆ§ âˆƒ k, len = 128 / 2 ^ k)
    (h1 : start < 256 âˆ§ start % (2 * len) = 0) : start + 2 * len â‰¤ 256 := by
      rcases h0 with âŸ¨hlen_gt, hlen_le, âŸ¨k, hkâŸ©âŸ©
      have: k â‰¤ 6 := by
        have k_le : 2^k <= 2^6 := by
          rw [hk] at hlen_gt
          have: 0 < 2^k := by
            apply pow_pos
            decide
          have := (Nat.le_div_iff_mul_le this).mp hlen_gt
          have h : 2 * 2^k â‰¤ 2 * 2^6 := this
          apply Nat.le_of_mul_le_mul_left h
          decide
        contrapose! k_le
        apply Nat.pow_lt_pow_right (by decide)
        exact k_le
      interval_cases k <;> simp_all <;> omega

  @[scalar_tac]
  theorem mul_range_add_in_bounds {len start : â„•}
    (h0 : 2 â‰¤ len âˆ§ len < 256 âˆ§ âˆƒ k, len = 2 * 2 ^ k)
    (h1 : start < 256 âˆ§ start % (2 * len) = 0) : start + 2 * len â‰¤ 256 := by
      rcases h0 with âŸ¨_, hlen_lt, âŸ¨k, hkâŸ©âŸ©
      have: k <= 6 := by
        contrapose hlen_lt
        simp_all
        have: 256 = 2 * 2^7 := by simp
        rw [this]
        apply Nat.mul_le_mul_left
        apply Nat.pow_le_pow_right (by decide)
        exact hlen_lt
      interval_cases k <;> simp_all <;> omega

end Notations

open Notations

set_option grind.warning false

@[reducible] def Q : Nat := 3329
@[reducible] def Zq := ZMod Q
@[reducible] def Polynomial (n : â„• := Q) := Vector (ZMod n) 256

abbrev Polynomial.length {m : â„•} (p : Polynomial m) : â„• := p.size

def Polynomial.zero (n := Q) : Polynomial n := Vector.replicate 256 0

instance {m} : HAdd (Polynomial m) (Polynomial m) (Polynomial m) where
  hAdd f g := Vector.map (fun i => f[i]! + g[i]!) (Vector.range 256)

instance {m} : HSub (Polynomial m) (Polynomial m) (Polynomial m) where
  hSub f g := Vector.map (fun i => f[i]! - g[i]!) (Vector.range 256)

def Polynomial.scalarMul (x : Polynomial n) (k : ZMod n) : Polynomial n := x.map fun v => v * k

instance {n} : HMul (Polynomial n) (ZMod n) (Polynomial n) where
  hMul := Polynomial.scalarMul

abbrev ğ”¹ := Vector Byte

/-! # Algorithm 3 -/
def bitsToBytes {â„“ : Nat} (b : Vector Bool (8 * â„“)) : ğ”¹ â„“ := Id.run do
  let mut B := Vector.replicate â„“ 0
  for h: i in [0:8*â„“] do
    B := B.set (i/8) (B[i/8] + (b[i].toNat * (2 ^(i%8)) : BitVec 8))
  pure B

/--
info: [10#8, 1#8]
-/
#guard_msgs in
#eval (@bitsToBytes 2 âŸ¨ âŸ¨ [false, true, false, true, false, false, false, false,
                            true, false, false, false, false, false, false, false] âŸ©,
                           by simp âŸ©).toList



/-! # Algorithm 4 -/
def bytesToBits {â„“ : Nat} (B : ğ”¹ â„“) : Vector Bool (8 * â„“) := Id.run do
  let mut C := B
  let mut b := Vector.replicate (8 * â„“) false
  for hi: i in [0:â„“] do
    for hj: j in [0:8] do
      b := b.set (8 * i + j) (C[i] % 2 â‰  0)
      C := C.set i (C[i] / 2)
  pure b

#assert
  let b : Vector Bool (8 * 2) :=
    âŸ¨ âŸ¨ [false, true, false, true, false, false, false, false,
         true, false, false, false, false, false, false, false] âŸ©, by simp âŸ©
  bytesToBits (bitsToBytes b) = b

/-! # Cryptographic Functions -/
-- TODO: would be good to move this to the top of the file

-- TODO: Is using bytesToBits and bitsToBytes the correct behavior?
-- TODO: use Lists rather than Arrays in Sha3? (why??)
def sha3_256 {n} (M : ğ”¹ n) : ğ”¹ 32 :=
  let M_bits := bytesToBits M
  bitsToBytes (Spec.SHA3_256 M_bits.toArray)

def sha3_512 {n} (M : ğ”¹ n) : ğ”¹ 64 :=
  let M_bits := bytesToBits M
  bitsToBytes (Spec.SHA3_512 M_bits.toArray)

def shake256 {n} (M : ğ”¹ n) (â„“ : â„•) : ğ”¹ â„“ :=
  let bits := (bytesToBits M).toArray
  bitsToBytes (Spec.SHAKE256 bits (8 * â„“))

/-! # Pseudorandom function (PRF) -/

abbrev Î— := {Î· : â„• // Î· âˆˆ ({2, 3} : Set â„•)}

def PRF (Î· : Î—) (s : ğ”¹ 32) (b : Byte) : ğ”¹ (64 * Î·) :=
  shake256 (s ++ Vector.singleton b) (64 * Î·)

/-! # Hash functions -/

def H {n} (s : ğ”¹ n) := sha3_256 s
def J {n} (s : ğ”¹ n) := shake256 s 32
def G {n} (s : ğ”¹ n) : ğ”¹ 32 Ã— ğ”¹ 32 :=
  let hash := sha3_512 s
  let a := hash.extract 0 32
  let b := hash.extract 32 64
  (a, b)


/-! # eXtendable-Output Function (XOF) -/

def XOF.init := Spec.SHAKE128.init

def XOF.absorb s (B : ğ”¹ â„“) :=
  Spec.SHAKE128.absorb s (bytesToBits B).toArray

def XOF.squeeze s â„“  :=
  let (s, Z) := Spec.SHAKE128.squeeze s (8 * â„“)
  (s, bitsToBytes Z)

/-! # Compress -/

abbrev m (d : â„•) : â„• := if d < 12 then 2^d else Q

def compress (d : {d: â„• // d < 12}) (x : Zq) : ZMod (m d) := âŒˆ ((2^d.val : â„š) / (Q : â„š)) * x.val âŒ‹

def Polynomial.compress (d : {d: â„• // d < 12}) (f : Polynomial) : Polynomial (m d) :=
  f.map (Spec.compress d)

/-! # Decompress -/
def decompress (d : {d: â„• // d < 12}) (y : ZMod (m d)) : Zq := âŒˆ ((Q : â„š) / (2^d.val : â„š)) * y.val âŒ‹

def Polynomial.decompress (d : {d: â„• // d < 12}) (f : Polynomial (m d)) : Polynomial :=
  f.map (Spec.decompress d)

/-! # Algorithm 5 -/
def byteEncode (d : â„•) (F : Polynomial (m d)) : ğ”¹ (32 * d) := Id.run do
  let mut b := Vector.replicate (256 * d) false
  for hi: i in [0:256] do
    have : i * d â‰¤ 255 * d := by scalar_tac +nonLin
    let mut a : â„• â† F[i].val
    for hj: j in [0:d] do
      b := b.set (i * d + j) (Bool.ofNat (a % 2))
      a := (a - b[i * d + j].toNat) / 2
  let B := bitsToBytes (Vector.cast (by ring_nf) b)
  pure B

/-! # Algorithm 6 -/
def byteDecode {d : â„•} (B : ğ”¹ (32 * d)) : Polynomial (m d) := Id.run do
  let b â† bytesToBits B
  let mut F := Polynomial.zero (m d)
  for hi: i in [0:256] do
    have : i * d â‰¤ 255 * d := by scalar_tac +nonLin
    F := F.set i (âˆ‘ (j : Fin d), b[i * d + j].toNat * 2^j.val)
  pure F

/-! # Algorithm 7 -/
def sampleNTT (B : ğ”¹ 34) := Id.run do
  let mut ctx := XOF.absorb XOF.init B
  let mut a := Polynomial.zero
  let mut j := 0
  while hj : j < 256 do
    let (ctx', C) := XOF.squeeze ctx 3 -- 24 bits
    ctx := ctx'
    let dâ‚ := C[0].val + 256 * (C[1].val % 16)
    let dâ‚‚ := C[1].val / 16 + 16 * C[2].val
    if dâ‚ < Q then
      a := a.set j dâ‚
      j := j + 1
    if h: dâ‚‚ < Q âˆ§ j < 256 then
      a := a.set j dâ‚‚
      j := j + 1
  pure a

/-! # Algorithm 8 -/

@[scalar_tac Î·.val]
theorem H.val (Î· : Î—) : Î·.val â‰¤ 3 := by
  have := Î·.property
  scalar_tac

def samplePolyCBD {Î·:Î—} (B : ğ”¹ (64 * Î·)) : Polynomial := Id.run do
  let b := bytesToBits B
  let mut f := Polynomial.zero
  for hi: i in [0:256] do
    have : 2 * i * Î· â‰¤ 510 * Î· := by scalar_tac +nonLin
    let x := âˆ‘ (j : Fin Î·), b[2 * i * Î· + j].toNat
    let y := âˆ‘ (j : Fin Î·), b[2 * i * Î· + Î· + j].toNat
    f := f.set i (x - y)
  pure f

def Î¶ : ZMod Q := 17

/-! # Algorithm 9 -/
def ntt (f : Polynomial) := Id.run do
  let mut f := f
  let mut i := 1
  for h0: len in [128 : >1 : /= 2] do
    for h1: start in [0 : 256 : 2*len] do
      let zeta := Î¶ ^ (bitRev 7 i)
      i := i + 1
      for h: j in [start : start+len] do
        let t := zeta * f[j + len]
        f := f.set (j + len) (f[j] - t)
        f := f.set j         (f[j] + t)
  pure f

/-! # Algorithm 10 -/
def invNtt (f : Polynomial) : Polynomial := Id.run do
  let mut f := f
  let mut i := 127
  for h0: len in [2 : <256 : *= 2] do
    for h1: start in [0:256:2*len] do
      let zeta := Î¶ ^ bitRev 7 i
      i := i - 1
      for h: j in [start:start+len] do
        let t := f[j]
        f := f.set j (t + f[j + len])
        f := f.set (j + len) (zeta * (f[j + len] - t))
  f := f * (3303 : Zq)
  pure f

/-! # Algorithm 12 -/
def baseCaseMultiply (a0 a1 b0 b1 Î³ : Zq) : Zq Ã— Zq :=
  let c0 := a0 * b0 + a1 * b1 * Î³
  let c1 := a0 * b1 + a1 * b0
  (c0, c1)

/-! # Algorithm 11 -/
def multiplyNTTs (f g : Polynomial) : Polynomial := Id.run do
  let mut h := Polynomial.zero
  for h: i in [0:128] do
    let (c0, c1) := baseCaseMultiply f[2 * i] f[2 * i + 1] g[2 * i] g[2 * i + 1] (Î¶^(2 * bitRev 7 i + 1))
    h := h.set (2 * i) c0
    h := h.set (2 * i + 1) c1
  pure h

/-! # ML-KEM parameter sets -/
-- TODO: would be good to move this to the top of the file

abbrev K := {k : â„• // k âˆˆ ({2, 3, 4}: Set â„•)}

inductive ParameterSet where
  | MLKEM512 : ParameterSet
  | MLKEM768 : ParameterSet
  | MLKEM1024 : ParameterSet

@[reducible, grind, scalar_tac_simps] def k (p : ParameterSet) : K :=
  match p with
  | .MLKEM512 => âŸ¨ 2, by scalar_tac âŸ©
  | .MLKEM768 => âŸ¨ 3, by scalar_tac âŸ©
  | .MLKEM1024 => âŸ¨ 4, by scalar_tac âŸ©

@[reducible, scalar_tac_simps] def Î·â‚ (p : ParameterSet) : Î— :=
  match p with
  | .MLKEM512 => âŸ¨ 3, by scalar_tac âŸ©
  | .MLKEM768 => âŸ¨ 2, by scalar_tac âŸ©
  | .MLKEM1024 => âŸ¨ 2, by scalar_tac âŸ©

def Î·â‚‚ : Î— := âŸ¨ 2, by scalar_tac âŸ©

@[reducible, scalar_tac_simps] def dáµ¤ (p : ParameterSet) : â„• :=
  match p with
  | .MLKEM512 => 10
  | .MLKEM768 => 10
  | .MLKEM1024 => 11

@[reducible, scalar_tac_simps] def dáµ¥ (p : ParameterSet) : â„• :=
  match p with
  | .MLKEM512 => 4
  | .MLKEM768 => 4
  | .MLKEM1024 => 5

/-! # Vectors and Matrices of Polynomials -/

@[reducible] def PolyVector (m : â„•) (k : K) := Vector (Polynomial m) k
def PolyVector.zero (m : â„•) (k : K) : PolyVector m k := Vector.replicate k (Polynomial.zero m)

def PolyVector.ofFn {m : â„•} {k : K} (f : Fin k â†’ Polynomial m) : PolyVector m k :=
  Vector.ofFn f

def PolyVector.set {k : K} {m : â„•} (v : PolyVector m k) (i : â„•) (f : Polynomial m) (h : i < k := by get_elem_tactic) : PolyVector m k :=
  Vector.set v i f

def PolyVector.add {k : K} {m : â„•} (v w : PolyVector m k) : PolyVector m k :=
  Vector.ofFn fun i => v[i] + w[i]

instance {k} {m} : HAdd (PolyVector m k) (PolyVector m k) (PolyVector m k) where
  hAdd f g := PolyVector.add f g

def PolyVector.mulNTT {k : K} (v w : PolyVector Q k) : PolyVector Q k :=
  Vector.ofFn fun i => multiplyNTTs v[i] w[i]

instance {k} : HMul (PolyVector Q k) (PolyVector Q k) (PolyVector Q k) where
  hMul f g := PolyVector.mulNTT f g

def PolyVector.NTT {k : K} (v : PolyVector Q k) : (PolyVector Q k) :=
  v.map ntt

def PolyVector.invNTT {k : K} (v : PolyVector Q k) : (PolyVector Q k) :=
  v.map invNtt

def PolyVector.compressVec {k : K} (d : {d: â„• // d < 12}) (v : PolyVector Q k) : PolyVector (m d) k :=
  v.map (Polynomial.compress d)

def PolyVector.decompress {k : K} (d : {d: â„• // d < 12}) (v : PolyVector (m d) k) : PolyVector Q k :=
  v.map (Polynomial.decompress d)

def PolyVector.byteEncode {k : K} (d : â„•) (v : PolyVector (m d) k) : ğ”¹ (k * (32 * d)) := Id.run do
  (Vector.flatten (v.map (Spec.byteEncode d))).cast (by grind)

def PolyVector.byteDecode {k : K} (d : â„•) (bytes : ğ”¹ (32 * d * k)) : PolyVector (m d) k :=
  PolyVector.ofFn fun i =>
    have : 32 * d * (i + 1) â‰¤ 32 * d * k := by simp_scalar
    Spec.byteDecode ((bytes.extract (32 * d * i) (32 * d * (i + 1))).cast (by simp_scalar; grind))

@[reducible] def PolyMatrix (n : â„•) (k : K) := Matrix (Fin k) (Fin k) (Polynomial n)
def PolyMatrix.zero (n : â„•) (k : K) : PolyMatrix n k := Matrix.of (fun _ _ â†¦ Polynomial.zero n)
def PolyMatrix.update {k : K} {n : â„•} (M : PolyMatrix n k) (i j : â„•) (val : Polynomial n)
  (hi : i < k := by get_elem_tactic) (_ : j < k := by get_elem_tactic) : PolyMatrix n k :=
  Matrix.updateRow M âŸ¨i, hiâŸ© (fun k => if k = j then val else (M âŸ¨i,hiâŸ© k))

/-- A âˆ˜ v -/
def PolyMatrix.MulVectorNTT {k : K} (A : PolyMatrix Q k) (v : PolyVector Q k) : PolyVector Q k :=
  PolyVector.ofFn fun i => Id.run do
    let mut wi := Polynomial.zero
    for hj: j in [0:k] do
      wi := wi + (multiplyNTTs (A âŸ¨i, by scalar_tacâŸ© âŸ¨j, by scalar_tacâŸ©) v[j])
    pure wi

/-- A âˆ˜ v -/
instance {k} : HMul (PolyMatrix Q k) (PolyVector Q k) (PolyVector Q k) where
  hMul A v := PolyMatrix.MulVectorNTT A v

/-- v^t âˆ˜ w -/
def PolyVector.innerProductNTT {k : K} (v : PolyVector Q k) (w : PolyVector Q k) : Polynomial := Id.run do
  let mut a := Polynomial.zero
  for hi: i in [0:k] do
    a := a + (multiplyNTTs v[i] w[i])
  pure a


/-! # Algorithm 13 -/

def kpke.keyGen (p : ParameterSet)
  (d : ğ”¹ 32) :
  ğ”¹ (384 * k p + 32) Ã— ğ”¹ (384 * k p) := Id.run do

  let (Ï, Ïƒ) := G (d ++ Vector.singleton (k p : Byte))
  let mut N : â„• := 0
  let mut A_hat := PolyMatrix.zero Q (k p)
  for hi: i in [0:k p] do
    for hj: j in [0:k p] do
      A_hat := PolyMatrix.update A_hat i j
        (sampleNTT (Ï ++ Vector.singleton (j : Byte) ++ Vector.singleton (i : Byte)))
  let mut s := PolyVector.zero Q (k p)
  for hi: i in [0:k p] do
    s := s.set i (samplePolyCBD (PRF (Î·â‚ p) Ïƒ N))
    N := N + 1
  let mut e := PolyVector.zero Q (k p)
  for hi: i in [0:k p] do
    e := e.set i (samplePolyCBD (PRF (Î·â‚ p) Ïƒ N))
    N := N + 1
  let s_hat := PolyVector.NTT s
  let e_hat := PolyVector.NTT e
  let t_hat := A_hat * s_hat + e_hat
  let ek_PKE := (PolyVector.byteEncode 12 t_hat ++ Ï).cast (by grind)
  let dk_PKE := (PolyVector.byteEncode 12 s_hat).cast (by grind)
  pure (ek_PKE, dk_PKE)

/-! # Algorithm 14 -/

def kpke.encrypt (p : ParameterSet)
  (ek_PKE : ğ”¹ (384 * k p + 32))
  (m : ğ”¹ 32)
  (r : ğ”¹ 32) :
  ğ”¹ (32 * (dáµ¤ p * k p + dáµ¥ p)) := Id.run do

  let mut N : â„• := 0
  let t_hat := PolyVector.byteDecode 12 ((ek_PKE.extract 0 (384 * k p)).cast (by grind))
  let Ï : ğ”¹ 32 := (ek_PKE.extract (384 * k p) (384 * k p + 32)).cast (by cases p <;> scalar_tac)
  let mut A_hat := PolyMatrix.zero Q (k p)
  for hi: i in [0:k p] do
    for hj: j in [0:k p] do
      A_hat := PolyMatrix.update A_hat i j
        (sampleNTT (Ï ++ Vector.replicate 1 (Byte.ofNat j) ++ Vector.replicate 1 (Byte.ofNat i)))
  let mut y := PolyVector.zero Q (k p)
  for hi: i in [0:k p] do
    y := y.set i (samplePolyCBD (PRF (Î·â‚ p) r (Byte.ofNat N)))
    N := N + 1
  let mut eâ‚ := PolyVector.zero Q (k p)
  for hi: i in [0:k p] do
    eâ‚ := eâ‚.set i (samplePolyCBD (PRF Î·â‚‚ r (Byte.ofNat N)))
    N := N + 1
  let eâ‚‚ := samplePolyCBD (PRF Î·â‚‚ r (Byte.ofNat N))
  let y_hat := PolyVector.NTT y
  let u := PolyVector.invNTT (A_hat.transpose * y_hat) + eâ‚
  let Î¼ := Polynomial.decompress âŸ¨1, by scalar_tacâŸ© (@byteDecode 1 m)
  let v := invNtt (PolyVector.innerProductNTT t_hat y_hat) + eâ‚‚ + Î¼
  let câ‚ := PolyVector.byteEncode (dáµ¤ p) (PolyVector.compressVec âŸ¨dáµ¤ p, by cases p <;> scalar_tacâŸ© u)
  let câ‚‚ := byteEncode (dáµ¥ p) (Polynomial.compress âŸ¨dáµ¥ p, by cases p <;> scalar_tacâŸ© v)
  (câ‚ ++ câ‚‚).cast (by cases p <;> scalar_tac)

/-! # Algorithm 15 -/

def kpke.decrypt (p : ParameterSet)
  (dk_PKE : ğ”¹ (384 * k p))
  (c : ğ”¹ (32 * (dáµ¤ p * k p + dáµ¥ p))) :
  ğ”¹ 32 :=

  let câ‚ : ğ”¹ (32 * dáµ¤ p * k p) := (c.extract 0 (32 * dáµ¤ p * k p)).cast (by grind)
  let câ‚‚ : ğ”¹ (32 * dáµ¥ p) := (c.extract (32 * dáµ¤ p * k p) (32 * (dáµ¤ p * k p + dáµ¥ p))).cast (by grind)
  let u' := PolyVector.decompress âŸ¨dáµ¤ p, by grindâŸ© (PolyVector.byteDecode (dáµ¤ p) câ‚)
  let v' := Polynomial.decompress âŸ¨dáµ¥ p, by grindâŸ© (byteDecode câ‚‚)
  let s_hat := PolyVector.byteDecode 12 dk_PKE
  let w := v' - invNtt (PolyVector.innerProductNTT s_hat (PolyVector.NTT u'))
  let m := byteEncode 1 (Polynomial.compress âŸ¨1, by grindâŸ© w)
  m

/-! # Algorithm 16 -/

def mlkem.keyGen_internal (p : ParameterSet)
  (d z : ğ”¹ 32) :
  ğ”¹ (384 * k p + 32) Ã— ğ”¹ (768 * k p + 96) :=

  let (ek_PKE, dk_PKE) := @kpke.keyGen p d
  let ek := ek_PKE.cast (by scalar_tac)
  let dk := (dk_PKE ++ ek ++ H ek ++ z).cast (by scalar_tac)
  (ek, dk)

/-! # Algorithm 17 -/

def mlkem.encaps_internal (p : ParameterSet)
  (ek : ğ”¹ (384 * k p + 32))
  (m : ğ”¹ 32) :
  ğ”¹ 32 Ã— ğ”¹ (32 * (dáµ¤ p * k p + dáµ¥ p)) :=

  let (K, r) := G (m ++ H ek)
  let c := kpke.encrypt p ek m r
  (K, c)

/-! # Algorithm 18 -/

def mlkem.decaps_internal (p : ParameterSet)
  (dk : ğ”¹ (768 * k p + 96))
  (c : ğ”¹ (32 * (dáµ¤ p * k p + dáµ¥ p))) :
  ğ”¹ 32 :=

  let dk_PKE : ğ”¹ (384 * k p) := (dk.extract 0 (384 * k p)).cast (by scalar_tac)
  let ek_PKE : ğ”¹ (384 * k p + 32) := (dk.extract (384 * k p) (768 * k p + 32)).cast (by scalar_tac)
  let h : ğ”¹ 32 := (dk.extract (768 * k p + 32) (768 * k p + 64)).cast (by scalar_tac)
  let z : ğ”¹ 32 := (dk.extract (768 * k p + 64) (768 * k p + 96)).cast (by scalar_tac)
  let m' := kpke.decrypt p dk_PKE c
  let (K', r') := G (m' ++ h)
  let K_bar := J (z ++ c)
  let c' := kpke.encrypt p ek_PKE m' r'
  let K' := if c â‰  c' then K_bar else K'
  K'

/-! # Algorithm 19 -/

/-! # Random byte generation -/
axiom randomBytes (length : â„•) : Option (ğ”¹ length)

/-
Not sure how to model randomness, so we simply take the random bytes as inputs.
-/
def mlkem.keygen (p : ParameterSet)
  (d z : Option (ğ”¹ 32)) :
  Option (ğ”¹ (384 * k p + 32) Ã— ğ”¹ (768 * k p + 96)) := do
  let d â† d
  let z â† z
  let (ek, dk) := mlkem.keyGen_internal p d z
  return (ek, dk)


/-! # Algorithm 20 -/

/-
Not sure how to model randomness, so we simply take the random bytes as inputs.
-/
def mlkem.encaps (p : ParameterSet)
  (ek : ğ”¹ (384 * k p + 32))
  (m : Option (ğ”¹ 32)) :
  Option (ğ”¹ 32 Ã— ğ”¹ (32 * (dáµ¤ p * k p + dáµ¥ p))) := do
  let m â† m
  let (K, c) := mlkem.encaps_internal p ek m
  return (K, c)

/-! # Algorithm 21 -/

def mlkem.decaps (p : ParameterSet)
  (dk : ğ”¹ (768 * k p + 96))
  (c : ğ”¹ (32 * (dáµ¤ p * k p + dáµ¥ p))) :
  ğ”¹ 32 :=

  let K' := mlkem.decaps_internal p dk c
  K'

end Symcrust.Spec
