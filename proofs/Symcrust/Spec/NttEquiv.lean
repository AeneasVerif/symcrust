import Lean
import Mathlib.Data.ZMod.Basic
import Mathlib.Algebra.Polynomial.Basic
import Mathlib.RingTheory.Coprime.Basic
import Mathlib.RingTheory.Ideal.Defs
import Mathlib.RingTheory.Ideal.Span
import Mathlib.RingTheory.Ideal.Quotient.Defs
import Mathlib.RingTheory.ZMod.UnitsCyclic
import Mathlib.Algebra.Ring.Prod

import Mathlib.Tactic.Ring
import Mathlib.Tactic.Linarith

import Init.Data.BitVec.Lemmas

set_option maxRecDepth 2500
set_option maxHeartbeats 50000
--set_option diagnostics true


/- In this file, "the thesis" refers to https://kannwischer.eu/thesis/phd-thesis-2023-01-03.pdf -/

/- The Kyber prime q and root of unity Œ∂ -/

@[simp]
def q := 3329

@[simp]
lemma q_isPrime : Nat.Prime q := by native_decide
instance : Fact (Nat.Prime q) := ‚ü®q_isPrime‚ü©

lemma q_nonzero : q ‚â† 0 := by trivial
lemma q_minus_one_fact : (q - 1) = 2^8 * 13 := rfl

example : (q-2)*q = 2^16 * 169 - 1 := by simp

def zeta := 17
theorem zeta_coprime : Nat.Coprime zeta q := by rfl


/-- Finite ring Zq --/

@[reducible]
def Zq := ZMod q
lemma Zq_cyclic : IsCyclic ZqÀ£ := by
  apply ZMod.isCyclic_units_prime q_isPrime

def Fq := Field Zq

namespace Zq
  open scoped ZMod
  open Nat

  def mk_elem (n : Fin q) : Zq := n
  def one : Zq := 1
  def Œ∂ : Zq := zeta

  lemma zeta_ne_one : Œ∂ ‚â† 1 := by trivial
  lemma zeta_ne_zero : Œ∂ ‚â† 0 := by trivial

  theorem zeta_isUnit : IsUnit Œ∂ := by
    rw [isUnit_iff_ne_zero]
    exact zeta_ne_zero

  lemma zeta_mul_inv_zeta_eq_one : Œ∂ * Œ∂‚Åª¬π = 1 := by
    apply div_self zeta_ne_zero

  lemma inv_zeta_mul_zeta_eq_one : Œ∂‚Åª¬π * Œ∂ = 1 := by
    rw [mul_comm]
    exact zeta_mul_inv_zeta_eq_one

  lemma inv_zeta_val : Œ∂‚Åª¬π = 1175 := by
    exact ZMod.inv_eq_of_mul_eq_one q Œ∂ 1175 (by rfl : Œ∂ * 1175 = 1)

  lemma inv_zeta_eq_zeta_pow : Œ∂‚Åª¬π = Œ∂ ^ 255 := by
    rw [inv_zeta_val] ; rfl

  theorem zeta_256_eq : Œ∂ ^ 256 = 1 := by rfl

  theorem zeta_128_eq : Œ∂ ^ 128 = - 1 := by rfl

  example : Œ∂ ^ 2 = 289 := by rfl

  example : Œ∂ ^ 13 = 939 := by rfl

  lemma zeta_pow_m_neq_one (m : Nat) (hu : m < 256) (hl : 0 < m) : Œ∂ ^ m ‚â† 1 := by
    decide +revert

  theorem zeta_order_eq_256 : orderOf Œ∂ = 256 := by
    apply (orderOf_eq_iff (by decide)).mpr
    constructor
    ¬∑ exact zeta_256_eq
    ¬∑ exact zeta_pow_m_neq_one

  lemma diff_mod (m k : Nat) (h‚ÇÄ : m ‚â• k) (h‚ÇÅ : (m - k) % 256 = 0) : (m % 256) = (k % 256) := by
    grind

  lemma zeta_pow_sub_zeta_pow_ne_zero (m k : Nat) (h : (m % 256) ‚â† (k % 256)) : Œ∂^m - Œ∂^k ‚â† 0 := by
    intro hyp
    by_cases h‚ÇÄ : k ‚â§ m
    ¬∑ have hmk : k + (m - k) = m := by grind
      have hzpow : Œ∂ ^ ((m-k) % 256) ‚â† 1 := by
        apply zeta_pow_m_neq_one (((m-k) % 256))
        ¬∑ grind
        ¬∑ by_contra h0
          simp at h0
          apply diff_mod at h0
          contradiction
          apply h‚ÇÄ
      have : Œ∂^k * (Œ∂^(m-k) - 1) = 0 := by
        calc
          Œ∂^k * (Œ∂^(m-k) - 1 ) = Œ∂^(k + (m-k)) - Œ∂^k := by ring
          _ = Œ∂^m - Œ∂^k := by rw [hmk]
          _ = 0 := by exact hyp
      have hzk : Œ∂^k ‚â† 0 := by apply pow_ne_zero k zeta_ne_zero
      apply eq_zero_or_eq_zero_of_mul_eq_zero at this
      cases this with
      | inl ll => contradiction
      | inr rr =>
        apply sub_eq_zero.mp at rr
        rw [‚Üê pow_mod_orderOf Œ∂ (m-k)] at rr
        simp [Zq.zeta_order_eq_256] at rr
        contradiction
    ¬∑ simp at h‚ÇÄ
      have hkm : m + (k - m ) = k := by grind
      have hzpow : Œ∂ ^ ((k-m) % 256) ‚â† 1 := by
        apply zeta_pow_m_neq_one (((k-m) % 256))
        ¬∑ grind
        ¬∑ by_contra h0
          simp at h0
          apply diff_mod at h0 ; symm at h0
          contradiction
          apply (le_of_lt h‚ÇÄ)
      have : Œ∂^m * (1-Œ∂^(k-m)) = 0 := by
        calc
          Œ∂^m * (1-Œ∂^(k-m)) = Œ∂^m - Œ∂^(m + (k-m)) := by ring
          _ = Œ∂^m - Œ∂^k := by rw [hkm]
          _ = 0 := by exact hyp
      have hzm : Œ∂^m ‚â† 0 := by apply pow_ne_zero m zeta_ne_zero
      apply eq_zero_or_eq_zero_of_mul_eq_zero at this
      cases this with
      | inl ll => contradiction
      | inr rr =>
        apply sub_eq_zero.mp at rr
        rw [‚Üê pow_mod_orderOf Œ∂ (k-m)] at rr
        simp [Zq.zeta_order_eq_256] at rr ; symm at rr
        contradiction

  theorem zeta_pow_sub_zeta_pow_isUnit (m k : Nat) (h : (m % 256) ‚â† (k % 256)) : IsUnit (Œ∂^m - Œ∂^k) := by
    have q_isPrime_fact : Fact (Nat.Prime q) := ‚ü®q_isPrime‚ü©
    have : (Œ∂^m - Œ∂^k) ^ (q-1) = 1 := by
      apply ZMod.pow_card_sub_one_eq_one (zeta_pow_sub_zeta_pow_ne_zero m k h)
    apply IsUnit.of_pow_eq_one this
    decide


end Zq

open Polynomial

@[reducible]
def Poly := Zq[X]

namespace Poly

  noncomputable
  def xn (n : Nat) : Zq[X] := monomial n Zq.one

  noncomputable def one : Zq[X] := monomial 0 1
  noncomputable def Œ∂ : Zq[X] := monomial 0 Zq.Œ∂
  noncomputable def Œ∂_inv : Zq[X]:= monomial 0 (ZMod.inv q Zq.Œ∂)

  theorem zeta_128_eq : Œ∂ ^ 128 = - one := by
    simp only [one, Œ∂, monomial_pow]
    simp [Zq.zeta_128_eq]

  theorem zeta_exp_p_128_eq (x : ‚Ñï) : Œ∂ ^ (x + 128) = - Œ∂ ^ x := by
    simp [pow_add Œ∂ x 128, zeta_128_eq, one]

  /- # The Kyber ring Rq -/
  def Rq := Zq[X] ‚ß∏ Ideal.span {xn 256 + 1}


  noncomputable
  def f_lk (l k : Nat) := xn (2 ^ (l + 1)) - Œ∂ ^ k

  theorem f_lk_mul (l k : Nat) : (f_lk l k) * (f_lk l (k + 128)) = f_lk (l+1) (2*k) := by
    simp [f_lk]
    ring_nf
    simp [zeta_128_eq, one, xn, monomial_pow, Zq.one]
    ring_nf


  /- # Two polynomials are coprime if m and k are not equal mod 256. -/
  theorem f_lk_coprime (l k m: Nat) (h: m % 256 ‚â† k % 256):
      IsCoprime (f_lk l k) (f_lk l m) := by
    have diffUnit : IsUnit (Zq.Œ∂^m - Zq.Œ∂^k) := by
      apply Zq.zeta_pow_sub_zeta_pow_isUnit
      exact h
    rw [f_lk, f_lk, IsCoprime]
    use monomial 0 (Ring.inverse (Zq.Œ∂^m - Zq.Œ∂^k))
    use -monomial 0 (Ring.inverse (Zq.Œ∂^m - Zq.Œ∂^k))
    rw [mul_sub, mul_sub, xn]
    ring_nf
    rw [‚Üê mul_sub_left_distrib, Œ∂]
    simp
    rw [‚Üê C.map_pow (Zq.Œ∂) m, ‚Üê C.map_pow (Zq.Œ∂), ‚Üê C.map_sub (Zq.Œ∂^m), ‚Üê C.map_mul, ‚Üê C.map_one]
    rw [ZMod.inv_mul_of_unit (Zq.Œ∂ ^ m - Zq.Œ∂ ^ k) diffUnit]

  /- # The corresponding ideals are coprime -/
  theorem f_lk_Ideals_coprime (l k m: Nat) (h: m % 256 ‚â† k % 256):
      IsCoprime (Ideal.span {f_lk l k}) (Ideal.span {f_lk l m}) := by
    apply (Ideal.isCoprime_span_singleton_iff (f_lk l k) (f_lk l m)).mpr
    exact f_lk_coprime l k m h

  /- # CRT for one decomposition from Rq -/
  /- Zq[X] ‚ß∏ (X^256 + 1) ‚âÉ+* Zq[X] ‚ß∏ (X^128 - Œ∂^64) √ó  Zq[X] ‚ß∏ (X^128 + Œ∂^64) -/
  noncomputable
  def crt_Rq_1 :
    (Zq[X] ‚ß∏ Ideal.span {f_lk 7 128}) ‚âÉ+*
    (Zq[X] ‚ß∏ Ideal.span {f_lk 6 64}) √ó (Zq[X] ‚ß∏ Ideal.span {f_lk 6 192}) := by
    have coprime : IsCoprime (Ideal.span {f_lk 6 64}) (Ideal.span {f_lk 6 192}) := by
      apply f_lk_Ideals_coprime
      grind
    have prod : (Ideal.span {f_lk 6 64}) * (Ideal.span {f_lk 6 192}) = Ideal.span {f_lk 7 128} := by
      simp [Ideal.span_singleton_mul_span_singleton (f_lk 6 64) (f_lk 6 192)]
      simp [f_lk_mul]
    rw [‚Üê prod]
    apply Ideal.quotientMulEquivQuotientProd (Ideal.span {f_lk 6 64}) (Ideal.span {f_lk 6 192}) coprime

  /- # CRT for one decomposition from any Rlk as long as the power at Œ∂ is even -/
  /- Zq[X] ‚ß∏ (X^(2^(l+1)) - Œ∂^(2k)) ‚âÉ+* Zq[X] ‚ß∏ (X^(2^l) - Œ∂^k) √ó  Zq[X] ‚ß∏ (X^(2^l) + Œ∂^k) -/
  noncomputable
  def crt_Rlk_1 (l k : Nat) :
    (Zq[X] ‚ß∏ Ideal.span {f_lk (l + 1) (2*k)}) ‚âÉ+*
    (Zq[X] ‚ß∏ Ideal.span {f_lk l k}) √ó (Zq[X] ‚ß∏ Ideal.span {f_lk l (k + 128)}) := by
    have coprime : IsCoprime (Ideal.span {f_lk l k}) (Ideal.span {f_lk l (k + 128)}) := by
      apply f_lk_Ideals_coprime
      grind
    have prod :
      (Ideal.span {f_lk l k}) * (Ideal.span {f_lk l (k + 128)}) =
       Ideal.span {f_lk (l + 1) (2*k)} := by
      simp [Ideal.span_singleton_mul_span_singleton (f_lk l k) (f_lk l (k + 128)), f_lk_mul]
    rw [‚Üê prod]
    apply Ideal.quotientMulEquivQuotientProd (Ideal.span {f_lk l k}) (Ideal.span {f_lk l (k + 128)}) coprime


  /- The BitRev‚Çá function from the ML-KEM specification [Section 4.3]
     "Define BitRev‚Çá(ùëñ) to be the integer represented by bit-reversing
      the unsigned 7-bit value that corresponds to the input integer
      ùëñ ‚àà {0,‚Ä¶,127}." -/
  def BitRev‚Çá (i : Fin 128) : Fin 128 :=
    have : i.val < 2 ^ 7 := by exact i.isLt
    let ibits := BitVec.ofNatLT i.val this
    (ibits.reverse).toFin

  #eval BitRev‚Çá 1

  example : BitRev‚Çá 3  = 96 := by rfl
  example : BitRev‚Çá 0  = 0 := by rfl
  example : BitRev‚Çá 127 = 127 := by rfl
  example : BitRev‚Çá 1  = 64 := by rfl
  example : BitRev‚Çá 2  = 32 := by rfl

  /- Define a more general version that allows the bitsize b of the
     integers to be any positive integer (instead of only b=7). -/
  def BitRev (b : ‚Ñï) (i : Fin (2 ^ b)) : Fin (2 ^ b) :=
    have : i.val < 2 ^ b := by exact i.isLt
    let ibits := BitVec.ofNatLT i.val this
    (ibits.reverse).toFin

  #eval BitRev 7 2

  example : BitRev 0 0 = 0 := by rfl
  example : BitRev 3 1 = 4 := by rfl
  example : BitRev 7 0 = 0 := by rfl
  example : BitRev 7 2 = 32 := by rfl

  lemma BitRev_equal : ‚àÄ i : Fin 128, BitRev‚Çá i = BitRev 7 i := by
    intro i; rfl

  lemma BitVec_reverse_reverse_eq {n : ‚Ñï} (v : BitVec n) : v.reverse.reverse = v := by sorry
    -- This seems to exist in Mathlib v4.25.

  lemma BitRev_inv (b : ‚Ñï) (i : Fin (2 ^ b)) : BitRev b (BitRev b i) = i := by
    simp [BitRev, BitVec_reverse_reverse_eq]

  lemma BitRev‚Çá_inv (i : Fin 128) : BitRev‚Çá (BitRev‚Çá i) = i := by
    decide +revert

  lemma BitRev_inj (b : ‚Ñï) (i j : Fin (2 ^ b)) (hij : i ‚â† j) : BitRev b i ‚â† BitRev b j := by
    intro h
    have h' : BitRev b (BitRev b i) = BitRev b (BitRev b j) := congr_arg (BitRev b) h
    rw [BitRev_inv, BitRev_inv] at h'
    exact hij h'


  /-- Bit reversal of an odd number (2i+1) equals bit reversal of the even number (2i)
      plus 2^(b-1), where b is the number of bits. This is because adding 1 sets the LSB,
      which becomes the MSB after reversal.
  -/
  lemma BitRev_odd_from_even (b : ‚Ñï) (hb : b > 0) (i : Fin (2 ^ (b - 1))) :
    let i‚ÇÇ : Fin (2 ^ b) := ‚ü®2 * i.val + 1, by
      have : 2 ^ b = 2 * 2 ^ (b - 1) := by
        cases b
        ¬∑ omega
        ¬∑ simp [Nat.pow_succ]; ring
      omega‚ü©
    let i‚ÇÅ : Fin (2 ^ b) := ‚ü®2 * i.val, by
      have : 2 ^ b = 2 * 2 ^ (b - 1) := by
        cases b
        ¬∑ omega
        ¬∑ simp [Nat.pow_succ]; ring
      omega‚ü©
    (BitRev b i‚ÇÇ).val = (BitRev b i‚ÇÅ).val + 2^(b - 1) := by
    intro i‚ÇÇ i‚ÇÅ
    have : Nat.testBit i‚ÇÅ.val 0 = false := by
      grind
    have : Nat.testBit i‚ÇÇ.val 0 = true := by
      grind
    have : i‚ÇÅ.val / 2 = i‚ÇÇ.val / 2 := by grind
    have : ‚àÄ j : ‚Ñï , Nat.testBit i‚ÇÅ.val (j+1) = Nat.testBit i‚ÇÇ.val (j+1) := by
      grind
    have : (BitVec.ofNat b i‚ÇÅ.val)[0] = false := by
      simp [i‚ÇÅ]
      sorry
    sorry

  #check BitVec.msb

  /- The NTT is a ring isomorphism from Rq to the product Tq of 128 rings defined by
     quadratic polynomials X^2 - Œ∂^k for some integer k. It works through repeated
     decomposition of the involved rings according to the following scheme.

     Rq = Z[X] ‚ß∏ (X^256 + 1) = Z[X] ‚ß∏ (X^256 - Œ∂^128)
        ‚âÖ Z[X] ‚ß∏ (X^128 - Œ∂^64) √ó Z[X] ‚ß∏ (X^128 + Œ∂^64) = Z[X] ‚ß∏ (X^128 - Œ∂^64) √ó Z[X] ‚ß∏ (X^128 - Œ∂^192)
        ‚âÖ Z[X] ‚ß∏ (X^64 - Œ∂^32) √ó Z[X] ‚ß∏ (X^64 - Œ∂^160) √ó Z[X] ‚ß∏ (X^64 - Œ∂^96) √ó Z[X] ‚ß∏ (X^64 - Œ∂^224)
        ‚âÖ ...

    Continuing this way leads to a scheme of exponents (x_exp, Œ∂_exp) for the
    polynomials X^x_exp - Œ∂^Œ∂_exp as follows:

    l=0: (256, 128)
    l=1: (128, 64), (128, 192)
    l=2: (64, 32), (64, 160), (64, 96), (64, 224)
    l=3: (32, 16), (32, 144), (32, 80), (32, 208), ...
    l=4: (16, 8), (16, 136), ...
    l=5: (8, 4), (8, 132), ...
    l=6: (4, 2), (4, 130), ...
    l=7: (2, 1), (2, 129), ...

    The second number, Œ∂_exp, if numbered with i = 0, ..., i = 2 ^ l - 1 in the order defined
    by the above decomposition is given by 2^(7-l) + (BitRev l i) * 2^(8-l).

    This means that the ring for for (l, i) decomposes as the product of the rings for (l+1, 2i) and (l+1, 2i+1).
  -/

  /- Define the polynomial that defines the i-th quotiont ring
     at level l down from Rq:
     fq (l, i) = X^x_exp - Œ∂^Œ∂_exp
               = X^(2^(8-l)) - Œ∂^(2^(7-l) + (BitRev l i)*2^(8-l)) -/

  --@[simp]
  def x_exp (l : Fin 8) : ‚Ñï := 2 ^ (8 - l.val)
  --@[simp]
  def Œ∂_exp (l : Fin 8) (i : Fin (2 ^ l.val)) : ‚Ñï :=
    (x_exp l)/2 + (BitRev l i).val * (x_exp l)

  lemma Œ∂_exp_ubound (l : Fin 8) (i : Fin (2 ^ l.val)) : Œ∂_exp l i < 2 ^ 8 := by
    decide +revert

  lemma Œ∂_exp_not_eq (l : Fin 8) (i j : Fin (2 ^ l.val)) (hij : i ‚â† j) : Œ∂_exp l i ‚â† Œ∂_exp l j := by
      intro h
      simp only [Œ∂_exp] at h
      have h_mul : (BitRev l i).val * x_exp l = (BitRev l j).val * x_exp l := by
        have : x_exp l / 2 + (BitRev l i).val * x_exp l = x_exp l / 2 + (BitRev l j).val * x_exp l := h
        linarith
      have hx_pos : 0 < x_exp l := by unfold x_exp; apply Nat.two_pow_pos
      have h_bitrev : (BitRev l i).val = (BitRev l j).val := Nat.eq_of_mul_eq_mul_right hx_pos h_mul
      have : BitRev l i = BitRev l j := Fin.ext h_bitrev
      exact BitRev_inj l i j hij this

  lemma Œ∂_exp_not_eq_mod (l : Fin 8) (i j : Fin (2 ^ l.val)) (hij : i ‚â† j) : (Œ∂_exp l i) % 256 ‚â† (Œ∂_exp l j) % 256 := by
      have hi : Œ∂_exp l i < 256 := by convert Œ∂_exp_ubound l i
      have hj : Œ∂_exp l j < 256 := by convert Œ∂_exp_ubound l j
      rw [Nat.mod_eq_of_lt hi, Nat.mod_eq_of_lt hj]; exact Œ∂_exp_not_eq l i j hij

  lemma Œ∂_exp_diff_IsUnit (l : Fin 8) (i j : Fin (2 ^ l.val)) (hij : i ‚â† j) : IsUnit (Zq.Œ∂^(Œ∂_exp l i) - Zq.Œ∂^(Œ∂_exp l j)) := by
      apply Zq.zeta_pow_sub_zeta_pow_isUnit
      exact Œ∂_exp_not_eq_mod l i j hij


  noncomputable
  def fq (l : Fin 8) (i : Fin (2 ^ l.val)) :=
    xn (x_exp l) - Œ∂ ^ (Œ∂_exp l i)

  example : fq 0 0 = xn 256 + 1 := by
    simp [fq, Œ∂_exp, x_exp, BitRev, zeta_128_eq, one]
  example : fq 7 0 = xn 2 - Œ∂ := by
    simp [fq, Œ∂_exp, x_exp, BitRev, BitVec.reverse]
  example : fq 7 2 = xn 2 - Œ∂ ^ 65 := by
    simp [fq, Œ∂_exp, x_exp, BitRev, BitVec.reverse, BitVec.msb, Nat.testBit]

  /- Define the i-th quotient ring at level l down from Rq defined by (fq l i). -/
  def Sq (l : Fin 8) (i : Fin (2 ^ l.val)) :=
    Zq[X] ‚ß∏ Ideal.span {fq l i}

  example : Sq 0 0 = (Zq[X] ‚ß∏ Ideal.span {xn 256 + 1}) := by
    simp [Sq, fq, Œ∂_exp, x_exp, zeta_128_eq, one]
  example : Sq 1 1 = (Zq[X] ‚ß∏ Ideal.span {xn 128 - Œ∂^192}) := by
    simp [Sq, fq, Œ∂_exp, x_exp, BitRev, BitVec.reverse, BitVec.msb]
  example : Sq 7 1 = (Zq[X] ‚ß∏ Ideal.span {xn 2 - Œ∂^129}) := by
    simp [Sq, fq, Œ∂_exp, x_exp, BitRev, BitVec.reverse, BitVec.msb, Nat.testBit]


  /- # Two polynomials (fq l i) and (fq l j) are coprime if i ‚â† j.-/
  theorem fq_coprime (l : Fin 8) (i j : Fin (2 ^ l.val)) (hij : i ‚â† j): IsCoprime (fq l i) (fq l j) := by
    rw [fq, fq, IsCoprime]
    use -monomial 0 (Ring.inverse (Zq.Œ∂^Œ∂_exp l i - Zq.Œ∂^Œ∂_exp l j))
    use monomial 0 (Ring.inverse (Zq.Œ∂^Œ∂_exp l i - Zq.Œ∂^Œ∂_exp l j))
    rw [mul_sub, mul_sub, xn]
    ring_nf
    rw [‚Üê mul_sub_left_distrib, Œ∂]
    simp
    rw [‚Üê C.map_pow (Zq.Œ∂) (Œ∂_exp l i), ‚Üê C.map_pow (Zq.Œ∂) (Œ∂_exp l j), ‚Üê C.map_sub (Zq.Œ∂^(Œ∂_exp l i)), ‚Üê C.map_mul, ‚Üê C.map_one]
    rw [ZMod.inv_mul_of_unit (Zq.Œ∂ ^ Œ∂_exp l i - Zq.Œ∂ ^ Œ∂_exp l j) (Œ∂_exp_diff_IsUnit l i j hij)]


  lemma fq_mul (l : Fin 8) (i : Fin (2 ^ l.val)) (hl : l.val < 7) :
    let l' : Fin 8 := ‚ü®l.val + 1, by omega‚ü©
    let i‚ÇÅ : Fin (2 ^ l'.val) := ‚ü®2 * i.val, by simp [l']; omega‚ü©
    let i‚ÇÇ : Fin (2 ^ l'.val) := ‚ü®2 * i.val + 1, by simp [l']; omega‚ü©
    fq l' i‚ÇÅ * fq l' i‚ÇÇ = fq l i := by
    intro l' i‚ÇÅ i‚ÇÇ
    simp only [fq]
    sorry


  noncomputable
  def crt_Sq_1 (l : Fin 8) (i : Fin (2 ^ l.val)) (hl : l.val < 7) :
    let l' : Fin 8 := ‚ü®l.val + 1, by omega‚ü©
    let i‚ÇÅ : Fin (2 ^ l'.val) := ‚ü®2 * i.val, by simp [l']; omega‚ü©
    let i‚ÇÇ : Fin (2 ^ l'.val) := ‚ü®2 * i.val + 1, by simp [l']; omega‚ü©
    Zq[X] ‚ß∏ Ideal.span {fq l i} ‚âÉ+* (Zq[X] ‚ß∏ Ideal.span {fq l' i‚ÇÅ}) √ó (Zq[X] ‚ß∏ Ideal.span {fq l' i‚ÇÇ}) :=
  by
    intro l' i‚ÇÅ i‚ÇÇ
    have coprime : IsCoprime (Ideal.span {fq l' i‚ÇÅ}) (Ideal.span {fq l' i‚ÇÇ}) := by
      rw [Ideal.isCoprime_span_singleton_iff]
      apply fq_coprime
      simp [i‚ÇÅ, i‚ÇÇ]
    have prod :
      (Ideal.span {fq l' i‚ÇÅ}) * (Ideal.span {fq l' i‚ÇÇ}) =
       Ideal.span {fq l i} := by
      rw [Ideal.span_singleton_mul_span_singleton]
      rw [fq_mul l i hl]
    rw [‚Üê prod]
    apply Ideal.quotientMulEquivQuotientProd (Ideal.span {fq l' i‚ÇÅ}) (Ideal.span {fq l' i‚ÇÇ}) coprime
